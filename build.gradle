apply plugin: 'java'

allprojects {

    apply plugin: 'java'
    apply plugin: 'idea'

    group = "spring.trace"
    version = "1.0.2"

    repositories {
        mavenCentral()
    }

    ext.springVersion = '4.0.5.RELEASE'
    ext.slf4jVersion = '1.7.7'
    ext.logBackVersion = '1.1.2'
    ext.servletApiVersion = '3.0.1'
}

subprojects {

    compileJava {
        sourceCompatibility = '1.7'
        targetCompatibility = '1.7'
        options.encoding = 'UTF-8'
    }

    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-core:1.9.5'
        testCompile 'org.hamcrest:hamcrest-library:1.3'
    }

    configurations {
        all*.exclude group: 'commons-logging'
    }

}

project(":spring-trace-core") {

    description = "spring-trace-core"

    dependencies {

        compile "org.springframework:spring-core:$springVersion",
                "org.springframework:spring-context:$springVersion",
                "org.springframework:spring-beans:$springVersion",
                "org.springframework:spring-expression:$springVersion",
                "org.springframework:spring-aop:$springVersion"

        // logging
        compile "org.slf4j:slf4j-api:$slf4jVersion",
                "org.slf4j:jcl-over-slf4j:$slf4jVersion",
                "ch.qos.logback:logback-classic:$logBackVersion"

        // spring integration test
        testCompile "org.springframework:spring-test:$springVersion"
    }

}

project(":spring-trace-web") {

    description = "spring-trace-web"

    dependencies {

        compile project(":spring-trace-core")
        compile "org.springframework:spring-web:$springVersion"

        // logging
        compile "org.slf4j:slf4j-api:$slf4jVersion",
                "org.slf4j:jcl-over-slf4j:$slf4jVersion",
                "ch.qos.logback:logback-classic:$logBackVersion"

        // spring integration test
        testCompile "org.springframework:spring-test:$springVersion"

        compile "javax.servlet:javax.servlet-api:$servletApiVersion"
    }

}

project(":sample-web") {

    description = "sample-web"

    apply plugin: 'war'

    dependencies {

        compile project(":spring-trace-web")

        compile "org.springframework:spring-core:$springVersion",
                "org.springframework:spring-context:$springVersion",
                "org.springframework:spring-context-support:$springVersion",
                "org.springframework:spring-jdbc:$springVersion",
                "org.springframework:spring-tx:$springVersion",
                "org.springframework:spring-orm:$springVersion",
                "org.springframework:spring-aspects:$springVersion",
                "org.springframework:spring-oxm:$springVersion",

                "org.springframework:spring-web:$springVersion",
                "org.springframework:spring-webmvc:$springVersion"

        // logging
        compile "org.slf4j:slf4j-api:$slf4jVersion",
                "ch.qos.logback:logback-classic:$logBackVersion"

        // spring integration test
        testCompile "org.springframework:spring-test:$springVersion"

        compile "javax.servlet:javax.servlet-api:$servletApiVersion"


    }

}


